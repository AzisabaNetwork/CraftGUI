plugins {
    id 'java'
    id("com.gradleup.shadow") version "8.3.0"
}

group = 'net.azisaba'
version = '1.0.2'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "azisaba-repo"
        url = "https://repo.azisaba.net/repository/maven-public/"
    }
    maven {
        name = "Lumine Releases"
        url = "https://mvn.lumine.io/repository/maven-public/"
    }
    maven {
        name = "CodeMC"
        url = uri("https://repo.codemc.io/repository/maven-public/")
    }
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT"
    compileOnly "net.azisaba:ItemStash:2.2.3"
    compileOnly "xyz.acrylicstyle:StorageBox:1.5.6+1.15.2"
    compileOnly "io.lumine.xikage:MythicMobs:4.12.0"
    implementation("de.tr7zw:item-nbt-api:2.15.2")
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate("de.tr7zw.changeme.nbtapi", "net.azisaba.craftgui.libs.nbtapi")
}

build {
    dependsOn(shadowJar)
}